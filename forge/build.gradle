buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        maven {
            url "https://repo.spongepowered.org/repository/maven-public/"
            content { includeGroup "org.spongepowered" }
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath group: 'gradle.plugin.com.matthewprenger', name: 'CurseGradle', version: '1.4.0'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'org.spongepowered.mixin'

version = "${mod_version}"
group = "${mod_group}"
archivesBaseName = "${mod_id}-forge"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: "snapshot", version: "20210309-1.16.5"

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=" + "${mod_id}".toString() + ".mixins.json"
            arg "-mixin.config=" + "${mod_id}_integrations".toString() + ".mixins.json"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                polymorph {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg "-mixin.config=" + "${mod_id}".toString() + ".mixins.json"
            arg "-mixin.config=" + "${mod_id}_integrations".toString() + ".mixins.json"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                polymorph {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
}


repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        url "https://www.cursemaven.com"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${version_forge}"

    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'

    implementation fg.deobf("mezz.jei:jei-1.16.5:${version_jei}")

    implementation fg.deobf("curse.maven:craftingcraft-237581:${file_craftingcraft}")
    implementation fg.deobf("curse.maven:craftingstation-318551:${file_craftingstation}")
    implementation fg.deobf("curse.maven:refinedstorage-243076:${file_refinedstorage}")
    implementation fg.deobf("curse.maven:refinedstorageaddons-272302:${file_refinedaddons}")
    implementation fg.deobf("curse.maven:prettypipes-376737:${file_prettypipes}")
    implementation fg.deobf("curse.maven:tomsstorage-378609:${file_tom}")
    implementation fg.deobf("curse.maven:simplestoragenetwork-268495:${file_ssn}")
    implementation fg.deobf("curse.maven:tinkers-74072:${file_tinkers}")
    implementation fg.deobf("curse.maven:mantle-74924:${file_mantle}")
    implementation fg.deobf("curse.maven:cyclic-239286:${file_cyclic}")
    implementation fg.deobf("curse.maven:sophisticatedbackpacks-422301:${file_sophisticated}")
    implementation fg.deobf("curse.maven:ae2-223794:${file_ae2}")
    implementation fg.deobf("curse.maven:extendedcrafting-268387:${file_extendedcrafting}")
    implementation fg.deobf("curse.maven:cucumber-272335:${file_cucumber}")
    implementation fg.deobf("curse.maven:fastsuite-475117:${file_fastsuite}")

    compileOnly fg.deobf("curse.maven:occultism-361026:${file_occultism}")
    compileOnly fg.deobf("curse.maven:fastworkbenchminus-394185:${file_fastworkbenchminus}")
    compileOnly fg.deobf("curse.maven:fastfurnaceminus-389989:${file_fastfurnaceminus}")

    compileOnly fg.deobf("curse.maven:fastworkbench-288885:${file_fastworkbench}")
    compileOnly fg.deobf("curse.maven:fastfurnace-299540:${file_fastfurnace}")
    implementation fg.deobf("curse.maven:placebo-283644:${file_placebo}")

    compileOnly fg.deobf("curse.maven:ironfurnaces-237664:${file_ironfurnaces}")

    runtimeOnly fg.deobf("curse.maven:vanillatweaks-272637:${file_vanillatweaks}")
    runtimeOnly fg.deobf("curse.maven:improvedstations-361348:${file_improvedstations}")
    runtimeOnly fg.deobf("curse.maven:patchouli-306770:3459118")
    runtimeOnly fg.deobf("curse.maven:curios-309927:3598943")
    runtimeOnly fg.deobf("curse.maven:geckolib-388172:3601135")
}

sourceSets {
    main {
        resources {
            srcDirs += '../docs'
        }
    }
}

jar {
    manifest {
        attributes(["Specification-Title": "${mod_name}",
                    "Specification-Vendor": "${mod_author}",
                    "Specification-Version": "${getArchiveVersion()}",
                    "Implementation-Title": "${mod_name}",
                    "Implementation-Version": "${getArchiveVersion()}",
                    "Implementation-Vendor" :"${mod_author}",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "MixinConfigs": "${mod_id}.mixins.json,${mod_id}_integrations.mixins.json"],)
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

tasks.build.dependsOn sourcesJar

def replaceResources = tasks.register("replaceResources", Copy) {
    it.outputs.upToDateWhen { false }

    it.from(sourceSets.main.resources) {
        include "META-INF/mods.toml"
        expand 'version': project.version, 'mod_id' : mod_id, 'mod_name': mod_name, 'mod_url': mod_url, 'mod_author': mod_author, 'mod_description': mod_description, 'mod_icon': mod_icon
    }
    it.into "$buildDir/resources/main/"
}

processResources {
    from("CHANGELOG.md")
    duplicatesStrategy(DuplicatesStrategy.FAIL)
    exclude('META-INF/mods.toml')
    configure { finalizedBy(replaceResources) }
}

curseforge {

    project {
        apiKey = findProperty('curseKey') ?: 0
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')
        addArtifact(sourcesJar)
        "${curse_versions}".split(',').each {
            addGameVersion "${it}"
        }
    }
}
